' Gambas module file

Public Sub Main()

  Dim Responder As Responder

  LogIt

  Select Case LCase(CGI!PATH_INFO)

    Case "/"
      If Request.Method = "POST" Then
        ScriptController.RunScript()

      Else If RequestUtils.ContainsParameter("gist")
        ScriptController.RunScript(GitHub.GetGist(RequestUtils.Parameter("gist")), True)

      Else
        Responder = New Responder
        Responder.LoadFile("Lite.html")
        Responder.Content = Replace(Responder.Content, "\"Hello World\"", "\"abc\"")
        Responder.Respond

      Endif

    Case "/help.html"
      Response.SendFile("help.html")

    Case "/favicon.ico"
      Response.SendFile("favicon.ico")

    Case "/gambas.png"
      Response.SendFile("gambas.png")

    Case "/playground.css"
      'Response.SendFile("playground.css")

    Case "/test"
      ScriptController.RunScript("Print \"Hello world from Gambas Playground Server!\"")

    Case "/bigtest"
      ScriptController.RunScript(File.Load("BigTest.gbs"), True)

    Case "/echo"
      Responder = New Responder
      Responder.Write(RequestUtils.Body)
      Responder.Respond

    Case "/info"
      Responder = New Responder
      Responder.WriteLine("User: " & User.Name)
      Responder.WriteLine("Time: " & Now)
      Responder.WriteLine("IP:   " & CGI!REMOTE_ADDR)
      Responder.Respond

    Default
      Status(404)
  End Select

End

Private Sub Status(Code As Integer, Optional Cause As String)

  Dim Responder As New Responder

  Responder.ContentType = "application/json"
  Responder.Status = Code
  If Cause Then Responder.WriteLine(JSON.Encode(["error": Cause]))
  Responder.Respond

End

Public Sub LogIt()

  Dim sStore As String

  If Not Exist(User.home &/ "logs") Then Mkdir User.home &/ "logs"
  If Exist(User.home &/ "logs/log.csv") Then sStore = File.Load(User.home &/ "logs/log.csv")
  sStore &= Format(Now, "dddd dd/mm/yyyy - hh:nn:ss") & "," & CGI!REMOTE_ADDR & "," & CGI!PATH_INFO & gb.NewLine
  File.Save(User.Home &/ "logs/log.csv", sStore)

End

