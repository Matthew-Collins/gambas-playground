' Gambas module file

Public Sub Main()

  Dim Stream As Stream

  LogIt
  
  Print(CGI!PATH_INFO)
  Select Case LCase(CGI!PATH_INFO)
  
    Case "/"
      If Request.Method = "POST" Then
        ScriptController.RunScript()

      Else If RequestUtils.ContainsParameter("gist")
        ScriptController.RunScript(GetGist(RequestUtils.Parameter("gist")), True)

      Else
        Status(405)

      Endif

    Case "/test"
      ScriptController.RunScript("Print \"Hello world from Gambas Playground Server!\"")

    Case "/bigtest"
      ScriptController.RunScript(File.Load("BigTest.gbs"), True)

    Case "/info"
      Stream = New Stream
      Stream.WriteLine("User: " & User.Name)
      Stream.WriteLine("Time: " & Now)
      Stream.WriteLine("IP:   " & CGI!REMOTE_ADDR)
      Stream.Send
      
    Default
      Status(404)
  End Select

End

Private Sub Status(Code As Integer, Optional Cause As String)

  Dim Stream As New Stream("application/json", Code)
  If Cause Then Stream.WriteLine(JSON.Encode(["error": Cause]))
  Stream.Send

End

Public Sub GetGist(GistId As String) As String                                  'To 'Get' stuff from the web

  Dim hClient As HttpClient                                                     'To create a HTTP Client
  Dim sResult As String
  Dim vResult As Variant
  Dim sContent As String
  Dim vFile As Variant

  hClient = New HttpClient As "hClient"                                         'Create a HTTP Client
  With hClient                                                                  'With the Client..
    .URL = "https://api.github.com/gists/" & GistId                             'Set up the URL
    .Async = False                                                              'No Asynchronous transmission
    .TimeOut = 60                                                               'Don't hang around waiting for more than 60 seconds
    .get                                                                        'Get the data
  End With

  If Lof(hClient) Then sResult = Read #hClient, Lof(hClient)                    'When all the data is downloaded store it in sResult

  vResult = JSON.Decode(sResult, False)

  For Each vFile In vResult["files"]
    sContent = vFile["content"]
    Break
  Next

  Return sContent

End

Public Sub LogIt()

  Dim sStore As String

  If Not Exist(User.home &/ "logs") Then Mkdir User.home &/ "logs"
  If Exist(User.home &/ "logs/log.csv") Then sStore = File.Load(User.home &/ "logs/log.csv")
  sStore &= Format(Now, "dddd dd/mm/yyyy - hh:nn:ss") & "," & CGI!REMOTE_ADDR & "," CGI!PATH_INFO & gb.NewLine
  File.Save(User.Home &/ "logs/log.csv", sStore)

End

