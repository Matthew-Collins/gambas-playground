' Gambas module file

Public Sub Main()

  Dim Responder As Responder

  LogIt
  
  Print(CGI!PATH_INFO)
  Select Case LCase(CGI!PATH_INFO)
  
    Case "/"
      If Request.Method = "POST" Then
        ScriptController.RunScript()

      Else If RequestUtils.ContainsParameter("gist")
        ScriptController.RunScript(GetGist(RequestUtils.Parameter("gist")), True)

      Else
        Responder = New Responder
        Responder.LoadFile("Lite.html")
        Responder.Respond

      Endif

    Case "/test"
      ScriptController.RunScript("Print \"Hello world from Gambas Playground Server!\"")

    Case "/bigtest"
      ScriptController.RunScript(File.Load("BigTest.gbs"), True)

    Case "/info"
      Responder = New Responder
      Responder.WriteLine("User: " & User.Name)
      Responder.WriteLine("Time: " & Now)
      Responder.WriteLine("IP:   " & CGI!REMOTE_ADDR)
      Responder.Respond
      
    Default
      Status(404)
  End Select

End

Private Sub Status(Code As Integer, Optional Cause As String)

  Dim Responder As New Responder("application/json", Code)
  If Cause Then Responder.WriteLine(JSON.Encode(["error": Cause]))
  Responder.Respond

End

Public Sub GetGist(GistId As String) As String                                  'To 'Get' stuff from the web

  Dim hClient As HttpClient                                                     'To create a HTTP Client
  Dim sResult As String
  Dim vResult As Variant
  Dim sContent As String
  Dim vFile As Variant

  hClient = New HttpClient As "hClient"                                         'Create a HTTP Client
  With hClient                                                                  'With the Client..
    .URL = "https://api.github.com/gists/" & GistId                             'Set up the URL
    .Async = False                                                              'No Asynchronous transmission
    .TimeOut = 60                                                               'Don't hang around waiting for more than 60 seconds
    .get                                                                        'Get the data
  End With

  If Lof(hClient) Then sResult = Read #hClient, Lof(hClient)                    'When all the data is downloaded store it in sResult

  vResult = JSON.Decode(sResult, False)

  For Each vFile In vResult["files"]
    sContent = vFile["content"]
    Break
  Next

  Return sContent

End

Public Sub LogIt()

  Dim sStore As String

  If Not Exist(User.home &/ "logs") Then Mkdir User.home &/ "logs"
  If Exist(User.home &/ "logs/log.csv") Then sStore = File.Load(User.home &/ "logs/log.csv")
  sStore &= Format(Now, "dddd dd/mm/yyyy - hh:nn:ss") & "," & CGI!REMOTE_ADDR & "," CGI!PATH_INFO & gb.NewLine
  File.Save(User.Home &/ "logs/log.csv", sStore)

End

