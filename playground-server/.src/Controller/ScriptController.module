' Gambas module file

Const TIMEOUT As Integer = 5000
Const STDOUT_LIMIT As Integer = 4096

Private $runner As Process
Private $timeout As New Timer As "Timeout"
Private $sContainerId As String
Private $iStdoutLen As Integer

Private Responder As New Responder

Public Sub RunScript(Optional Code As String = "", ShowCodeDefault As Boolean = False)

  If Code = "" Then
    Code = Trim(RequestUtils.Body)
  Endif

  If RequestUtils.IsParameter("ShowCode", ShowCodeDefault) Then
    Responder.WriteLine("Code:" & gb.NewLine)
    Responder.WriteLine(Code)
    Responder.Respond
  End If

  Responder.WriteLine("Result:")
  If Not Code Then
    Responder.Respond
    Return
  Endif

  Shell "docker create --cap-drop=ALL --cpus=1 --memory=16m --net=none --pids-limit=20 --security-opt=no-new-privileges --interactive gbs3" To $sContainerId
  $sContainerId = Trim($sContainerId)

  Print "Container: " & $sContainerId

  If Not Exist(User.home &/ "scripts") Then Mkdir User.home &/ "scripts"
  File.Save(User.home &/ "scripts" &/ $sContainerId & ".gbs", Code)
  Shell "docker cp " &/ User.home &/ "scripts" &/ $sContainerId & ".gbs " & $sContainerId & ":script.gbs" Wait
  Try Kill User.home &/ "scripts" &/ $sContainerId & ".gbs"

  $runner = Shell "docker start --attach --interactive " & $sContainerId For Read As "Runner"

  $timeout.Delay = TIMEOUT
  $timeout.Start

End

Public Sub Main()

  RunScript()

End

Public Sub Runner_Read()

  Dim len As Integer = Lof($runner)
  Dim data As String

  If len + $iStdoutLen > STDOUT_LIMIT Then
    data = Read #$runner, - STDOUT_LIMIT - $iStdoutLen
    Responder.WriteLine(data)
    Responder.WriteLine("-----8<----- Output has been truncated (" & STDOUT_LIMIT & " bytes max) -----8<-----")
    $runner.Close
    $runner.Kill
  Else
    data = Read #$runner, len
    Responder.Write(data)
    $iStdoutLen += len
  Endif

End

Public Sub Runner_Error(stderr As String)

  Responder.Write(stderr)

End

Public Sub Runner_Kill()

  Responder.Respond
  $timeout.Stop
  Exec ["docker", "rm", "--force", $sContainerId] For Read

End

Public Sub Timeout_Timer()

  Stream.WriteLine("\nProcess timed out !")
  Exec ["docker", "kill", $sContainerId] For Read
  $runner.Kill

End


